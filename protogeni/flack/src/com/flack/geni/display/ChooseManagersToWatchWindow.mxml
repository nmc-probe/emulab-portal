<?xml version="1.0" encoding="utf-8"?>
<components:PopupTitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
							 xmlns:s="library://ns.adobe.com/flex/spark" 
							 xmlns:mx="library://ns.adobe.com/flex/mx"
							 xmlns:components="com.flack.shared.display.components.*"
							 skinClass="com.flack.shared.display.skins.TitleWindowSkinNoCloseButton"
							 width="300"
							 height="300"
							 title="List resources from..."
							 initialize="init()"
							 defaultButton="{okButton}">
	<components:layout>
		<s:VerticalLayout gap="4"
						  paddingTop="4"
						  paddingBottom="4"
						  paddingLeft="4"
						  paddingRight="4" />
	</components:layout>
	<fx:Declarations>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.flack.geni.GeniCache;
			import com.flack.geni.GeniMain;
			import com.flack.geni.resources.sites.GeniManager;
			import com.flack.geni.resources.sites.GeniManagerCollection;
			
			import mx.collections.ArrayCollection;
			
			import spark.collections.Sort;
			import spark.collections.SortField;
			
			public var callAfter:Function;
			
			private var managers:ArrayCollection;
			
			public function init():void
			{
				// Get user preferences
				var allWatched:Boolean = true;
				var managersToWatch:Dictionary = GeniCache.getManagersToWatch();
				
				managers = new ArrayCollection();
				for each(var addManager:GeniManager in GeniMain.geniUniverse.managers.collection)
					managers.addItem(addManager);
				var sort:Sort = new Sort();
				var sortfield:SortField = new SortField("hrn");
				sort.fields = [sortfield];
				managers.sort = sort;
				managers.refresh();

				for each(var manager:GeniManager in managers)
				{
					var managerCheckbox:CheckBox = new CheckBox();
					managerCheckbox.percentWidth = 100;
					managerCheckbox.label = manager.hrn;
					managerCheckbox.toolTip = manager.url;
					if(managersToWatch[manager.id.full] != null)
					{
						managerCheckbox.selected = managersToWatch[manager.id.full];
						if(allWatched && !managerCheckbox.selected)
							allWatched = false;
					}
					else
						managerCheckbox.selected = true;
					managerItems.addElement(managerCheckbox);
				}
				selectAllCheckbox.selected = allWatched;
				okButton.setFocus();
			}
			
			public function selectAllChange():void
			{
				for(var i:int = 0; i < managerItems.numElements; i++)
					(managerItems.getElementAt(i) as CheckBox).selected = selectAllCheckbox.selected;
			}
			
			public function finish():void
			{
				var managersToWatch:Dictionary = new Dictionary();
				var watchedManagers:GeniManagerCollection = new GeniManagerCollection();
				for(var i:int = 0; i < managerItems.numElements; i++)
				{
					var manager:GeniManager = managers.getItemAt(i) as GeniManager;
					var managerWatched:Boolean = (managerItems.getElementAt(i) as CheckBox).selected
					managersToWatch[manager.id.full] = managerWatched;
					if(managerWatched)
						watchedManagers.add(manager);
				}
				// Set the cache for future reference and continue;
				GeniCache.setManagersToWatch(managersToWatch);
				GeniCache.setAskWhichManagersToWatch(!dontAskAgainButton.selected);
				callAfter(watchedManagers);
				closeWindow();
			}
			
		]]>
	</fx:Script>
	<s:CheckBox id="selectAllCheckbox"
				label="All/None"
				fontWeight="bold"
				toolTip="List resources at all or none of the managers"
				change="selectAllChange()" />
	<s:TileGroup id="managerItems"
				 width="100%"
				 height="100%"
				 orientation="columns"
				 clipAndEnableScrolling="true" />
	<components:controlBarLayout>
		<s:HorizontalLayout verticalAlign="middle"
							gap="4"
							paddingTop="4"
							paddingBottom="4"
							paddingLeft="4"
							paddingRight="4" />
	</components:controlBarLayout>
	<components:controlBarContent>
		<s:Button id="okButton"
				  label="Continue"
				  click="finish()" />
		<s:CheckBox id="dontAskAgainButton"
					label="Don't ask again" />
	</components:controlBarContent>
</components:PopupTitleWindow>
